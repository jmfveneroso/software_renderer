#version 330 core

// Input vertex data, different for all executions of this shader.
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec2 vertexUV;

out VertexData {
  vec3 position;
  vec2 UV;
  vec3 normal_cameraspace;
  float visibility;
  float steepness;
  vec3 position_cameraspace;
} out_data;

// Values that stay constant for the whole mesh.
uniform mat4 MVP;
uniform mat4 V;
uniform mat4 M;
uniform mat3 MV3x3;
uniform sampler2DRect NormalsSampler;
uniform sampler2DRect HeightMapSampler;
uniform ivec2 PlayerPosition;
uniform ivec2 buffer_top_left;
uniform int TILE_SIZE;
uniform int PURE_TILE_SIZE;
uniform int CLIPMAP_SIZE;
uniform int MAX_HEIGHT;

void main(){
  out_data.position = vertexPosition_modelspace;

  ivec2 toroidal_coords = ivec2(out_data.position.x, out_data.position.z);
  ivec2 buffer_coords = (toroidal_coords + buffer_top_left + CLIPMAP_SIZE + 1) % (CLIPMAP_SIZE + 1);
  float height = -(MAX_HEIGHT / 2) + MAX_HEIGHT * texelFetch(HeightMapSampler, buffer_coords).r;
  vec3 normal = texelFetch(NormalsSampler, buffer_coords).rgb * 2 - 1;

  out_data.position.x *= TILE_SIZE;
  out_data.position.z *= TILE_SIZE;
  out_data.position.y = height;

  vec3 position_worldspace = (M * vec4(out_data.position, 1)).xyz;

  gl_Position = MVP * vec4(out_data.position, 1);
  
  vec2 p = position_worldspace.xz / PURE_TILE_SIZE;
  out_data.UV = p / 8;
  out_data.normal_cameraspace = (V * M * vec4(normal, 0)).xyz; 

  out_data.position_cameraspace = (V * M * vec4(out_data.position, 1)).xyz;
  float distance = length(out_data.position_cameraspace.xyz);
  out_data.visibility = clamp(distance / 1000000, 0.0, 1.0);

  out_data.position = (M * vec4(out_data.position, 1)).xyz;
  out_data.steepness = (clamp(1 - normal.y, 0, 0.3)) / 0.3;
}
